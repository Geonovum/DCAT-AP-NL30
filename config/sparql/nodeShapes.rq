PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX opt-beg: <http://definities.geostandaarden.nl/dcat-ap-nl/id/concept/>

SELECT ?targetClass ?propertyShape ?property ?propertyName ?description ?class ?datatype ?nodeShapeName ?nodeShapeSeeAlso ?nodeKind ?minCount ?maxCount ?optionaliteit
WHERE {
  ?nodeShape a sh:NodeShape;
    sh:targetClass ?targetClass .
  # TODO shape name
  OPTIONAL {
    ?nodeShape sh:property ?propertyShape .

    ?propertyShape sh:path ?property .

    OPTIONAL {
      ?propertyShape sh:name ?propName .
    }
    OPTIONAL {
      ?propertyShape sh:description ?description .
    }
    OPTIONAL {
      ?propertyShape sh:class ?class .
    }
    OPTIONAL {
      ?propertyShape sh:datatype ?datatype .
    }
    OPTIONAL {
      ?propertyShape sh:node/sh:name ?nodeShName . 
    }
    OPTIONAL {
      ?propertyShape sh:node/rdfs:seeAlso ?nodeShSeeAlso .
    }
    OPTIONAL {
      ?propertyShape sh:nodeKind ?nodeKind .
    }
    OPTIONAL {
      ?propertyShape sh:minCount ?min .
    }
    OPTIONAL {
      ?propertyShape sh:maxCount ?max .
    }
    OPTIONAL {
      ?propertyShape opt-beg:optionaliteit/skos:notation ?optionaliteit .
    }
  }

  BIND(STR(?propName) as ?propertyName)

  BIND(STR(?nodeShName) as ?nodeShapeName)

  BIND(STR(?nodeShSeeAlso) as ?nodeShapeSeeAlso)

  BIND(STR(?min) as ?minCount)

  BIND(STR(?max) as ?maxCount)
}
ORDER BY ?propertyName

